plugins {
    id 'java'
    id "io.freefair.lombok" version "6.4.3"
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

group 'ai.sapper.hcdc'
version '1.1-SNAPSHOT'


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "ai.sapper.hcdc"
            from components.java

            pom {
                name = 'HCDC Change Manager'
                description = 'Hadoop Change Data capture framework'
                url = 'https://sapper.ai'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subhagho'
                        name = 'Subho Ghosh'
                        email = 'subho.ghosh@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:subhagho/hcdc.git'
                    developerConnection = 'scm:git:git@github.com:subhagho/hcdc.git'
                    url = 'https://sapper.ai'
                }
            }
        }
    }

}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2"
        }
        if (details.requested.name == 'commons-logging') {
            details.useTarget "org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2"
        }
    }
}

apply plugin: 'maven-publish'
subprojects {

    apply plugin: 'java'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    group 'ai.sapper.hcdc'
    version '1.1-SNAPSHOT'

    java {
        withSourcesJar()
    }

    repositories {
        mavenCentral()
        maven {
            url layout.buildDirectory.dir(repoDirSnapshot)
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroup "ai.sapper"
            }
        }
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version
                from components.java
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = layout.buildDirectory.dir(repoDirRelease)
                def snapshotsRepoUrl = layout.buildDirectory.dir(repoDirSnapshot)
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
        implementation 'org.slf4j:slf4j-log4j12:1.7.36'
        implementation 'org.apache.logging.log4j:log4j-api:2.17.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
        implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2'
        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.projectlombok:lombok:1.18.24'
        implementation 'commons-beanutils:commons-beanutils:1.9.4'
        implementation 'commons-io:commons-io:2.11.0'

        implementation 'org.apache.commons:commons-configuration2:2.7'
        // https://mvnrepository.com/artifact/com.beust/jcommander
        implementation group: 'com.beust', name: 'jcommander', version: '1.82'

        implementation 'com.fasterxml.jackson.core:jackson-core:2.13.2'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.2'

        implementation 'com.google.protobuf:protobuf-java:2.5.0'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

}